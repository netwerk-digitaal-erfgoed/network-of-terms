PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX text: <http://jena.apache.org/text#>
PREFIX vrank: <http://purl.org/voc/vrank#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel_ext ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote_en , ?scopeNote_nl ;
        skos:broader ?broader ;
        vrank:simpleRank ?score .
    ?broader skos:prefLabel ?broader_prefLabel .
}
WHERE {
    {
        SELECT ?uri (MAX(?sc) as ?score) WHERE {
            (?uri ?sc) text:query (gn:name gn:alternateName ?query) .
            ?uri a gn:Feature ;
                gn:featureClass ?featureClass ;
                gn:countryCode ?countryCode .
            # Limit results to places (P), localities (L), administrative levels (A) and water surfaces (H).
            VALUES ?featureClass { gn:P gn:L gn:A gn:H }
            # Optionally limit to specific countries based on the dataset.
            FILTER(
                ?datasetUri = <https://www.geonames.org>
                || ?countryCode IN ("NL", "BE", "DE")
            )
        }
        GROUP BY ?uri
        ORDER BY DESC(?score)
        #LIMIT#
    }

    ?uri gn:name ?prefLabel ;
        gn:countryCode ?countryCode .

    BIND(CONCAT(?prefLabel," (",UCASE(?countryCode),")") as ?prefLabel_ext)

    OPTIONAL {
        ?uri gn:alternateName ?altLabel .
        FILTER(?altLabel != "")
    }

    OPTIONAL {
        ?uri ?parents ?broader .
        ?broader gn:name ?broader_prefLabel
        VALUES ?parents { gn:parentADM1 gn:parentADM2 }
        # filter out the circular links in the converted data
        FILTER(?uri != ?broader)
    }

    ?uri gn:featureClass ?featureClass .
    VALUES (?featureClass ?scopeNote_en ?scopeNote_nl) {
        (gn:A "Administrative boundary"@en "Bestuurlijke eenheid"@nl)
        (gn:H "Hydrographic"@en "Water"@nl)
        (gn:L "Area"@en "Gebied"@nl)
        (gn:P "Populated place"@en "Plaats"@nl)
        (gn:R "Road/railroad"@en "Infrastructuur"@nl)
        (gn:S "Spot"@en "Object"@nl)
        (gn:T "Hypsographic"@en "ReliÃ«f"@nl)
        (gn:U "Undersea"@en "Onder water"@nl)
        (gn:V "Vegetation"@en "Vegetatie"@nl)
    }
}
