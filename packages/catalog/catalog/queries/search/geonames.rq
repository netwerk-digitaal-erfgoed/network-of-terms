PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX text: <http://jena.apache.org/text#>
PREFIX vrank: <http://purl.org/voc/vrank#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel_ext ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote_en , ?scopeNote_nl ;
        skos:broader ?broader ;
        vrank:simpleRank ?score .
    ?broader skos:prefLabel ?broader_prefLabel .
}
WHERE {
    {
        SELECT ?uri ?featureClass ?countryCode (MAX(?sc) + IF(CONTAINS(CONCAT(" ", LCASE(?query), " "), CONCAT(" ", LCASE(?countryCode), " ")), 2.0, 0) AS ?score) WHERE {
            (?uri ?sc) text:query (gn:name gn:alternateName ?query) .
            ?uri a gn:Feature ;
                gn:featureClass ?featureClass ;
                gn:countryCode ?countryCode .
            # Limit results to places (P), localities (L), administrative levels (A) and water surfaces (H).
            VALUES ?featureClass { gn:P gn:L gn:A gn:H }
            # Country filter from URI fragment (e.g. #NL-BE-DE), or allow all if no fragment
            BIND(COALESCE(STRAFTER(STR(?datasetUri), "#"), "") AS ?countriesFragment)
            FILTER(?countriesFragment = "" || CONTAINS(UCASE(?countriesFragment), ?countryCode))
        }
        GROUP BY ?uri ?featureClass ?countryCode
        ORDER BY DESC(?score)
        #LIMIT#
    }

    ?uri gn:name ?prefLabel .

    BIND(CONCAT(?prefLabel," (",UCASE(?countryCode),")") as ?prefLabel_ext)

    VALUES (?featureClass ?scopeNote_en ?scopeNote_nl) {
        (gn:A "Administrative boundary"@en "Bestuurlijke eenheid"@nl)
        (gn:H "Hydrographic"@en "Water"@nl)
        (gn:L "Area"@en "Gebied"@nl)
        (gn:P "Populated place"@en "Plaats"@nl)
    }

    OPTIONAL {
        ?uri gn:alternateName ?altLabel .
        FILTER(?altLabel != "")
    }

    OPTIONAL {
        ?uri ?parents ?broader .
        ?broader gn:name ?broader_prefLabel
        VALUES ?parents { gn:parentADM1 gn:parentADM2 }
        # filter out the circular links in the converted data
        FILTER(?uri != ?broader)
    }
}
