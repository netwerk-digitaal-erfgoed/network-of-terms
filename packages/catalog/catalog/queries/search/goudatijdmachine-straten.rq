PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gtm: <https://www.goudatijdmachine.nl/def#>
PREFIX sdo: <https://schema.org/>
PREFIX hg: <http://rdf.histograph.io/>
PREFIX omeka: <http://omeka.org/s/vocabs/o#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
CONSTRUCT {
  ?piduri a skos:Concept ;
    skos:prefLabel ?prefLabel ;
    skos:altLabel ?altLabel;
    skos:scopeNote ?scopeNote ;
    skos:related ?related_uri ;
    skos:exactMatch ?exactMatch_uri .
  ?related_uri skos:prefLabel ?related_prefLabel .
  ?exactMatch_uri skos:prefLabel ?exactMatch_label .
} WHERE {
  ?uri sdo:name|sdo:alternateName ?item .
  ?uri a gtm:Straat .
  FILTER(REGEX(STR(?item), ?query, "i"))
  ?uri owl:sameAs ?piduri .
  ?uri sdo:name ?prefLabel .
  OPTIONAL {
    ?uri gtm:genoemdNaar ?genoemdNaar .
  }
  BIND(
    IF(
      EXISTS {
        ?uri omeka:item_set <https://n2t.net/ark:/60537/bd75pg> 
      },
      "Verdwenen straat",
      "Straat"
    ) AS ?typeLabel
  )
  BIND(
    COALESCE(CONCAT(", ", CONCAT(
          LCASE(SUBSTR(STR(?genoemdNaar), 1, 1)),
          SUBSTR(STR(?genoemdNaar), 2)
        )), "") AS ?namedSuffix
  )
  BIND(
    CONCAT(?typeLabel, " in Gouda", ?namedSuffix) AS ?scopeNote
  )
  OPTIONAL {
    ?uri sdo:alternateName ?altLabel .
  }
  OPTIONAL {
    ?uri hg:absorbedBy|hg:absorbed ?related_uri .
    ?related_uri a gtm:Straat ;
                 sdo:name ?related_prefLabel .
    FILTER (?uri != ?related_uri )
  }
  OPTIONAL {
    ?uri rdfs:seeAlso ?exactMatch_uri .
    ?exactMatch_uri omeka:label ?exactMatch_label .
  }
}