PREFIX hg: <http://rdf.histograph.io/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote ;
        skos:exactMatch ?exactMatch_uri .
}
WHERE {
    GRAPH <https://lod.uba.uva.nl/ATM/Adamlink/graphs/streets> {
        {
            SELECT ?uri WHERE {
                ?uri a hg:Street ;
                    ?predicate ?label .
                VALUES ?predicate { skos:prefLabel skos:altLabel }
                ?label <bif:contains> ?virtuosoQuery .
            }
            #LIMIT#
        }

        OPTIONAL { ?uri skos:prefLabel ?prefLabel }
        OPTIONAL { ?uri skos:altLabel ?altLabel }
        OPTIONAL {
            ?uri hg:liesIn <http://sws.geonames.org/2759793/> # Gemeente Amsterdam
            BIND("Straat in Gemeente Amsterdam" AS ?scopeNote)
        }
        OPTIONAL { ?uri owl:sameAs ?exactMatch_uri } # Has no labels.
    }
}
