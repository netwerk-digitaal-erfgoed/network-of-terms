PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote_nl, ?scopeNote_en ;
        skos:exactMatch ?exactMatch_uri ;
        rdfs:seeAlso ?seeAlso .
}
WHERE {
    {
        SELECT DISTINCT ?uri
            (GROUP_CONCAT(DISTINCT ?schema_description; separator="; ") as ?descriptions)
            (GROUP_CONCAT(DISTINCT ?additionalType_nl; separator=", ") as ?additionalTypes_nl)
            (GROUP_CONCAT(DISTINCT ?additionalType_en; separator=", ") as ?additionalTypes_en)
            ?birthPlace_nl
            ?birthPlace_en
            ?deathPlace_nl
            ?deathPlace_en
        WHERE {
            ?uri ?predicate ?label .
            ?label <bif:contains> ?virtuosoQuery .
            VALUES ?predicate { schema:name schema:alternateName }

            ?uri a ?type .
            VALUES ?type { schema:Person schema:Organization }

            OPTIONAL { ?uri schema:description ?schema_description . }

            OPTIONAL {
                ?uri schema:birthPlace ?birthPlace_nl .
                FILTER(langMatches(lang(?birthPlace_nl), "nl"))
            }
            OPTIONAL {
                ?uri schema:birthPlace ?birthPlace_en .
                FILTER(langMatches(lang(?birthPlace_en), "en"))
            }

            OPTIONAL {
                ?uri schema:deathPlace ?deathPlace_nl .
                FILTER(langMatches(lang(?deathPlace_nl), "nl"))
            }
            OPTIONAL {
                ?uri schema:deathPlace ?deathPlace_en .
                FILTER(langMatches(lang(?deathPlace_en), "en"))
            }

            OPTIONAL {
                ?uri schema:additionalType ?additionalType_nl .
                FILTER(langMatches(lang(?additionalType_nl), "nl"))
            }
            OPTIONAL {
                ?uri schema:additionalType ?additionalType_en .
                FILTER(langMatches(lang(?additionalType_en), "en"))
            }
        }
        #LIMIT#
    }

    OPTIONAL { ?uri schema:name ?prefLabel . }
    OPTIONAL { ?uri schema:alternateName ?altLabel . }
    OPTIONAL { ?uri schema:birthDate ?birthDate . }
    OPTIONAL { ?uri schema:deathDate ?deathDate . }
    OPTIONAL { ?uri rdfs:seeAlso ?exactMatch_uri . }

    BIND(
        STRLANG(
            CONCAT(
                IF(BOUND(?birthDate) || STRLEN(?birthPlace_nl) > 0,
                    CONCAT(
                        IF(BOUND(?birthDate), CONCAT(?birthDate, " "), ""),
                        IF(STRLEN(?birthPlace_nl) > 0, CONCAT("(", ?birthPlace_nl, ") "), ""),
                        IF(BOUND(?deathDate) || STRLEN(?deathPlace_nl) > 0," - "," – ; ")),
                        ""
                    ),
                IF(BOUND(?deathDate) || STRLEN(?deathPlace_nl) > 0,
                    CONCAT(
                        IF(BOUND(?birthDate) || STRLEN(?birthPlace_nl) > 0, "", "– "),
                        IF(BOUND(?deathDate), CONCAT(?deathDate, " "), ""),
                        IF(STRLEN(?deathPlace_nl) > 0, CONCAT("(", ?deathPlace_nl, ")"), ""),
                        "; "),
                        ""
                    ),
                IF(STRLEN(?additionalTypes_nl) > 0, CONCAT(?additionalTypes_nl, ". "), ""),
                IF(STRLEN(?descriptions) > 0, CONCAT(?descriptions, ". "), "")
            ),
            "nl"
        ) as ?scopeNote_nl
    )
    BIND(
        STRLANG(
            CONCAT(
                IF(BOUND(?birthDate) || STRLEN(?birthPlace_en) > 0,
                    CONCAT(
                        IF(BOUND(?birthDate), CONCAT(?birthDate, " "), ""),
                        IF(STRLEN(?birthPlace_en) > 0, CONCAT("(", ?birthPlace_en, ") "), ""),
                        IF(BOUND(?deathDate) || STRLEN(?deathPlace_en) >0, " - ", " – ; ")),
                        ""
                    ),
                IF(BOUND(?deathDate) || STRLEN(?deathPlace_en) > 0,
                    CONCAT(
                        IF(BOUND(?birthDate) || STRLEN(?birthPlace_en) > 0, "", "– "),
                        IF(BOUND(?deathDate), CONCAT(?deathDate, " "), ""),
                        IF(STRLEN(?deathPlace_en) > 0, CONCAT("(", ?deathPlace_en, ")"), ""),
                        "; "),
                        ""
                    ),
                IF(STRLEN(?additionalTypes_en) > 0, CONCAT(?additionalTypes_en, ". "), ""),
                IF(STRLEN(?descriptions) > 0, CONCAT(?descriptions, ". "), "")
            ),
            "en"
        ) as ?scopeNote_en
    )
    BIND(STRAFTER(STR(?uri), "https://data.rkd.nl/artists/") AS ?identifier)
    BIND(IRI(CONCAT("https://rkd.nl/explore/artists/", ?identifier)) AS ?seeAlso)
}
