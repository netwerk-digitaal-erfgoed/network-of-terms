PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX som: <https://data.muziekschatten.nl/som/>

CONSTRUCT {
    ?uri a skos:Concept;
        skos:prefLabel ?prefLabel;
        skos:scopeNote ?scopeNote_nl, ?scopeNote_en;
        skos:broader ?broader_uri ;
        skos:exactMatch ?exactMatch_uri .
    ?broader_uri skos:prefLabel ?broader_schema_name .
}
WHERE {
    VALUES ?uri { ?uris }

    ?uri a skos:Concept
    BIND(<https://data.muziekschatten.nl/#onderwerpen> AS ?datasetUri).

    OPTIONAL {
        ?uri schema:name ?prefLabel .
    }
    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri schema:name ?broader_schema_name .
    }
    OPTIONAL {
        ?uri schema:keywords ?schema_keywords
    }
    OPTIONAL {
        ?uri som:DC ?dc
    }
    BIND(
        STRLANG(
            COALESCE(
                IF(BOUND(?schema_keywords) && BOUND(?dc), CONCAT("Trefwoorden: ", ?schema_keywords, " | ", "Dewey: ", ?dc), ?noScopeNote),
                IF(BOUND(?schema_keywords), CONCAT("Trefwoorden: ", ?schema_keywords), ?noScopeNote),
                IF(BOUND(?dc), CONCAT("Dewey: ", ?dc), ?noScopeNote)
            ),
            "nl"
        ) AS ?scopeNote_nl
    )

    BIND(
        STRLANG(
            COALESCE(
                IF(BOUND(?schema_keywords) && BOUND(?dc), CONCAT("Keywords: ", ?schema_keywords, " | ", "Dewey: ", ?dc), ?noScopeNote),
                IF(BOUND(?schema_keywords), CONCAT("Keywords: ", ?schema_keywords), ?noScopeNote),
                IF(BOUND(?dc), CONCAT("Dewey: ", ?dc), ?noScopeNote)
            ),
            "en"
        ) AS ?scopeNote_en
    )

    OPTIONAL { ?uri skos:exactMatch ?exactMatch_uri } # Has no labels.
}
LIMIT 1000
