PREFIX sdo: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gtm: <https://www.goudatijdmachine.nl/def#>
PREFIX hg: <http://rdf.histograph.io/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX omeka: <http://omeka.org/s/vocabs/o#>
CONSTRUCT {
  ?uri a skos:Concept ;
    skos:prefLabel ?prefLabel ;
    skos:altLabel ?altLabel ;
    skos:scopeNote ?scopeNote ;
    skos:related ?related_uri ;
    skos:exactMatch ?exactMatch_uri .
  ?related_uri skos:prefLabel ?related_prefLabel .
  ?exactMatch_uri skos:prefLabel ?exactMatch_prefLabel .
} WHERE {
  # For example:
  # Vuilsteeg: <https://n2t.net/ark:/60537/bIwfWk>
  # Kapelstraat: <https://n2t.net/ark:/60537/b4ApaO>
  VALUES ?uri { ?uris }
  ?uri a gtm:Straat ;
       sdo:identifier ?identifier ;
       sdo:name ?prefLabel .
  OPTIONAL {
    ?uri sdo:alternateName ?altLabel
  }
  BIND ( CONCAT( IF ( EXISTS {
          ?uri omeka:item_set <https://n2t.net/ark:/60537/bd75pg> 
        }, "Verdwenen straat", "Straat" )," in Gouda") AS ?scopeNote )
  OPTIONAL {
    ?uri hg:absorbedBy|hg:absorbed ?related_uri .
    ?related_uri a gtm:Straat ;
                 sdo:name ?related_prefLabel .
    FILTER (?uri != ?related_uri )
  }
  OPTIONAL {
    ?uri owl:sameAs ?exactMatch_uri .
    ?exactMatch_uri sdo:name ?exactMatch_prefLabel .
    FILTER(?exactMatch_uri != ?uri)
  }
}
LIMIT 100