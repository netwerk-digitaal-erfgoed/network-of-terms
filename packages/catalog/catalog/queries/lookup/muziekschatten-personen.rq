PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdaw: <http://rdaregistry.info/Elements/w/>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX som: <https://data.muziekschatten.nl/som/>

CONSTRUCT {
    ?uri a skos:Concept;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?schema_alternateName ;
        skos:scopeNote ?schema_hasOccupation ;
        skos:exactMatch ?exactMatch_uri .
}
WHERE {
    VALUES ?uri { ?uris }

    ?uri a schema:Person .

    OPTIONAL { ?uri schema:name ?schema_name }
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
    OPTIONAL {
        ?uri schema:hasOccupation ?hasOccupation .
        BIND(REPLACE(?hasOccupation, ";;;;;;;;;;;;", "") AS ?schema_hasOccupation)
    }

    OPTIONAL { ?uri schema:familyName ?schema_familyName }
    OPTIONAL { ?uri schema:givenName ?schema_givenName }
    BIND(
        COALESCE(
            IF(STRLEN(?schema_familyName) > 0 && STRLEN(?schema_givenName) > 0, CONCAT(?schema_familyName, ", ", ?schema_givenName), ?noName),
            ?schema_familyName,
            ?schema_givenName,
            ?schema_name # Fallback
        ) AS ?name
    )
    OPTIONAL { ?uri som:GDAT ?som_birthYear }
    OPTIONAL { ?uri som:SDAT ?som_deathYear }
    BIND(
        COALESCE(
            IF(BOUND(?som_birthYear) && BOUND(?som_deathYear), CONCAT(?som_birthYear, "-", ?som_deathYear), ?noDate),
            IF(BOUND(?som_birthYear), CONCAT(?som_birthYear, "-"), ?noDate),
            IF(BOUND(?som_deathYear), CONCAT("-", ?som_deathYear), ?noDate),
            ""
        ) AS ?dates
    )
    BIND(CONCAT(?name, IF(?dates != "", CONCAT(" (", ?dates, ")"), "")) AS ?prefLabel)
    OPTIONAL { ?uri owl:sameAs ?exactMatch_uri } # Has no labels.
}
LIMIT 1000
