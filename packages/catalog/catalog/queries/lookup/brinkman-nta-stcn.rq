PREFIX schema: <http://schema.org/>
# Do not remove this prefix - KB data uses old version of SKOS
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX void: <http://rdfs.org/ns/void#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:prefLabel ?rdfs_label ;
        skos:altLabel ?altLabel ;
        skos:altLabel ?schema_alternateName ;
        skos:altLabel ?schema_name ;
        skos:hiddenLabel ?hiddenLabel ;
        skos:scopeNote ?scopeNote ;
        skos:scopeNote ?schema_description ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:related ?related_uri ;
        skos:inScheme ?datasetUri ;
        skos:exactMatch ?exactMatch_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    ?related_uri skos:prefLabel ?related_prefLabel .
    ?exactMatch_uri skos:prefLabel ?exactMatch_prefLabel .
}
WHERE {
    # For example:
    # Concept in Brinkman: <http://data.bibliotheken.nl/id/thes/p317203789>
    # Person in NTA: <http://data.bibliotheken.nl/id/thes/p06854796X>
    # Organization in STCN: <http://data.bibliotheken.nl/id/thes/p075537222>
    VALUES ?uri { ?uris }

    ?uri a ?type .
    VALUES ?type { skos:Concept schema:Person schema:Organization } .

    ?uri schema:mainEntityOfPage/schema:isPartOf | foaf:isPrimaryTopicOf/void:inDataset ?datasetUri .

    # For Brinkman
    OPTIONAL {
        OPTIONAL {
            ?uri skos:prefLabel ?prefLabel .
            FILTER(LANG(?prefLabel) = "nl" )
        }
        OPTIONAL {
            ?uri skos:altLabel ?altLabel .
            FILTER(LANG(?altLabel) = "nl")
        }
        OPTIONAL {
            ?uri skos:hiddenLabel ?hiddenLabel .
            FILTER(LANG(?hiddenLabel) = "nl")
        }
        OPTIONAL {
            ?uri skos:scopeNote ?scopeNote .
            FILTER(LANG(?scopeNote) = "nl")
        }
        OPTIONAL {
            ?uri skos:broader ?broader_uri .
            ?broader_uri skos:prefLabel ?broader_prefLabel .
            FILTER(LANG(?broader_prefLabel) = "nl")
        }
        OPTIONAL {
            ?uri skos:narrower ?narrower_uri .
            ?narrower_uri skos:prefLabel ?narrower_prefLabel .
            FILTER(LANG(?narrower_prefLabel) = "nl")
        }
        OPTIONAL {
            ?uri skos:related ?related_uri .
            ?related_uri skos:prefLabel ?related_prefLabel .
            FILTER(LANG(?related_prefLabel) = "nl")
        }
        OPTIONAL { ?uri skos:exactMatch ?exactMatch_uri } # Has no labels.
    }

    # For NTA
    OPTIONAL {
        OPTIONAL { ?uri rdfs:label ?rdfs_label }
        OPTIONAL { ?uri schema:name ?schema_name }
        OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
        OPTIONAL { ?uri schema:description ?schema_description }
        OPTIONAL {
            ?uri ?matchPredicate ?exactMatch_uri .
            VALUES ?matchPredicate { owl:sameAs schema:sameAs }
            OPTIONAL {
                ?exactMatch_uri rdfs:label ?exactMatch_prefLabel .
            }
        }
    }

    # For Corp
    OPTIONAL {
        OPTIONAL { ?uri schema:name ?rdfs_label }
        OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
        OPTIONAL { ?uri schema:description ?schema_description }
        OPTIONAL {
            ?uri ?matchPredicate ?exactMatch_uri .
            VALUES ?matchPredicate { owl:sameAs schema:sameAs }
            OPTIONAL {
                ?exactMatch_uri rdfs:label ?exactMatch_prefLabel .
            }
        }
    }

    # For STCN
    OPTIONAL {
        OPTIONAL { ?uri schema:location/schema:address/schema:addressLocality ?scopeNote }
        OPTIONAL { ?uri schema:sameAs ?exactMatch_uri } # Has no labels.
    }
}
LIMIT 1000
