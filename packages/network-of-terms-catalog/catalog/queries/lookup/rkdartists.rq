PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote ;
        rdfs:seeAlso ?seeAlso .
}
WHERE {
    # For example:
    # Rembrandt: <https://data.rkd.nl/artists/66219>
    # Vincent van Gogh: <https://data.rkd.nl/artists/32439>
    VALUES ?uri { ?uris }
    {
        SELECT ?uri
            (GROUP_CONCAT(DISTINCT ?schema_description; separator="; ") as ?descriptions)
            (GROUP_CONCAT(DISTINCT ?birthPlace; separator="/") as ?birthPlaces)
            (GROUP_CONCAT(DISTINCT ?deathPlace; separator="/") as ?deathPlaces)
            (GROUP_CONCAT(DISTINCT ?additionalType; separator=", ") as ?additionalTypes)
        WHERE {
            ?uri a ?type .
            VALUES ?type { schema:Person schema:Organization }

            OPTIONAL { ?uri schema:description ?schema_description . }
            OPTIONAL { ?uri schema:birthPlace ?birthPlace .
                FILTER(langMatches(lang(?birthPlace),"en")) }
            OPTIONAL { ?uri schema:deathPlace ?deathPlace .
                FILTER(langMatches(lang(?deathPlace),"en"))}
            OPTIONAL { ?uri schema:additionalType ?additionalType .
                FILTER(langMatches(lang(?additionalType),"en"))}

        } GROUP BY ?uri
    }

    OPTIONAL { ?uri schema:name ?prefLabel . }
    OPTIONAL { ?uri schema:alternateName ?altLabel . }
    OPTIONAL { ?uri schema:birthDate ?birthDate . }
    OPTIONAL { ?uri schema:deathDate ?deathDate . }

    BIND(
        CONCAT(
          IF(BOUND(?birthDate) || STRLEN(?birthPlaces) > 0 ,
            CONCAT(
              IF(BOUND(?birthDate), CONCAT(?birthDate, " "), ""),
              IF(STRLEN(?birthPlaces) > 0, CONCAT("(", ?birthPlaces, ") "), ""),
              IF(BOUND(?deathDate) || STRLEN(?deathPlaces) > 0," - "," - ; ")),
            ""),
          IF(BOUND(?deathDate) || STRLEN(?deathPlaces) > 0,
            CONCAT(
              IF(BOUND(?birthDate) || STRLEN(?birthPlaces) > 0 , "", "- "),
              IF(BOUND(?deathDate), CONCAT(?deathDate, " "), ""),
              IF(STRLEN(?deathPlaces) > 0, CONCAT("(", ?deathPlaces, ")"), ""),
              "; "),
            ""),
            IF(STRLEN(?additionalTypes) > 0, CONCAT(?additionalTypes, ". "), ""),
            IF(STRLEN(?descriptions) > 0, CONCAT(?descriptions, ". "), "")
        ) as ?scopeNote
    )
    BIND(STRAFTER(STR(?uri), "https://data.rkd.nl/artists/") AS ?identifier)
    BIND(IRI(CONCAT("https://rkd.nl/explore/artists/", ?identifier)) AS ?seeAlso)
}
LIMIT 1000
