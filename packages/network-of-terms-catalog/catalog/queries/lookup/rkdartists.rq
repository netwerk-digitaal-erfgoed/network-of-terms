PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?schema_name ;
        skos:altLabel ?schema_alternateName ;
        skos:scopeNote ?schema_description ;
        rdfs:seeAlso ?rdfs_seeAlso .
}
WHERE {
    # For example:
    # Rembrandt: <https://data.rkd.nl/artists/66219>
    # Vincent van Gogh: <https://data.rkd.nl/artists/32439>
    VALUES ?uri { ?uris }

    ?uri a ?type .
    VALUES ?type { schema:Person schema:Organization } .

    OPTIONAL { ?uri schema:name ?schema_name . }
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName . }
    OPTIONAL { ?uri schema:description ?schema_description . }
    BIND(STRAFTER(STR(?uri), "https://data.rkd.nl/artists/") AS ?identifier)
    BIND(IRI(CONCAT("https://rkd.nl/explore/artists/", ?identifier)) AS ?rdfs_seeAlso)
}
LIMIT 1000
