PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gn: <https://www.geonames.org/ontology#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel_ext ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote_en ;
        skos:broader ?broader .
    ?broader skos:prefLabel ?broader_prefLabel .
}
WHERE {
    # https://sws.geonames.org/2752375 (Koudekerke)
    VALUES ?uri { ?uris }

    ?uri a gn:Feature ;
        gn:countryCode ?countryCode ;
        gn:name ?prefLabel .

    BIND(CONCAT(?prefLabel," (",UCASE(?countryCode),")") as ?prefLabel_ext)

    OPTIONAL {
        ?uri gn:alternateName ?altLabel .
        FILTER(?altLabel != "")
    }

    OPTIONAL {
        ?uri ?parents ?broader .
        ?broader gn:name ?broader_prefLabel
        VALUES ?parents { gn:parentADM1 gn:parentADM2 }
        # filter out the circular links in the converted data
        FILTER(?uri != ?broader)
    }

    OPTIONAL {
        ?uri gn:featureCode/gn:name ?scopeNote .
        # scopeNote is always in English.
        BIND(STRLANG(?scopeNote, "en") as ?scopeNote_en)
    }
}
LIMIT 1000
