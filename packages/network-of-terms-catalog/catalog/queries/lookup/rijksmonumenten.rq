PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ceosp: <https://linkeddata.cultureelerfgoed.nl/def/ceosp/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <https://schema.org/>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?rijksmonumentnummer_tn ;
        skos:altLabel ?adres_lang ;
        skos:scopeNote ?scopeNote ;
        rdfs:seeAlso ?monument_url .
}
WHERE {
    VALUES ?uri { ?uris }

    ?uri schema:name ?naam ;
        schema:identifier ?rijksmonumentnummer ;
        schema:category ?monumentaard ;
        schema:postalCode ?postcode ;
        schema:additionalType ?functie ;
        schema:addressLocality ?woonplaats ;
        schema:State ?provincie ;
        schema:sameAs ?monument_url .

    BIND(CONCAT("Rijksmonumentnummer ", ?rijksmonumentnummer) as ?rijksmonumentnummer_tn)

    # Aggregate and get the minimum address as ?adres2
    {
        SELECT ?uri (MIN(?adres) AS ?adres2)
        WHERE {
            OPTIONAL { ?uri schema:address ?adres . }
        }
        GROUP BY ?uri
    }

    BIND(
        CONCAT(
            "Naam: ", STR(?naam),
            " / Oorspronkelijke functie: ", STR(?functie),
            " / Type monument: ", STR(?monumentaard)
        ) AS ?scopeNote
    )

    BIND(
        CONCAT(
            IF(BOUND(?adres2), CONCAT(STR(?adres2), " "), ""),
            STR(?postcode), " ", STR(?woonplaats)
        ) AS ?adres_lang
    )
}
LIMIT 200
