PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX som: <https://data.muziekschatten.nl/som/>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?schema_name ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote ;
        skos:scopeNote ?schema_hasOccupation ;
        skos:broader ?broader_uri ;
        skos:inScheme ?datasetUri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
}
WHERE {
    # For example:
    # Subject: <https://data.muziekschatten.nl/som/d781655>
    # Subject: <https://data.muziekschatten.nl/som/d78165>
    # Person: <https://data.muziekschatten.nl/som/b7bc6d966287e0f0e0f491752ff65ba5>
    # Person: <https://data.muziekschatten.nl/som/4275e51d7d94b224928da9b7707a170a>
    # Uitvoeringsmedium: <https://data.muziekschatten.nl/som/um2603>
    VALUES ?uri { ?uris }

    ?uri a ?type .
    VALUES ?type { skos:Concept schema:Person } .

    # For Uitvoeringsmedia
    OPTIONAL {
        ?uri skos:inScheme ?datasetUri ;
            skos:prefLabel ?prefLabel .

        FILTER(?datasetUri = <https://data.muziekschatten.nl/som/Uitvoeringsmedium>)

        FILTER(LANG(?prefLabel) = "nl")

        OPTIONAL {
            ?uri skos:scopeNote ?scopeNote
        }

        OPTIONAL {
            ?uri skos:altLabel ?altLabel .
            FILTER(LANG(?altLabel) = "nl")
        }

        OPTIONAL {
            ?uri skos:broader ?broader_uri .
            ?broader_uri skos:prefLabel ?broader_prefLabel .
            FILTER(LANG(?broader_prefLabel) = "nl")
        }
    }

    # For subjects
    OPTIONAL {
        ?uri a skos:Concept
        BIND(<https://data.muziekschatten.nl/#onderwerpen> AS ?datasetUri).

        OPTIONAL {
            ?uri schema:name ?schema_name .
            FILTER(LANG(?schema_name) = "nl")
        }

        OPTIONAL {
            ?uri skos:broader ?broader_uri .
            ?broader_uri schema:name ?broader_prefLabel .
            FILTER(LANG(?broader_prefLabel) = "nl")
        }
        OPTIONAL {
            ?uri schema:keywords ?schema_keywords
        }
        OPTIONAL {
            ?uri som:DC ?dc
        }
        BIND(
            COALESCE(
                IF(BOUND(?schema_keywords) && BOUND(?dc), CONCAT("Trefwoorden: ", ?schema_keywords, " | ", "Dewey: ", ?dc), ?noScopeNote),
                IF(BOUND(?schema_keywords), CONCAT("Trefwoorden: ", ?schema_keywords), ?noScopeNote),
                IF(BOUND(?dc), CONCAT("Dewey: ", ?dc), ?noScopeNote)
            ) AS ?scopeNote
        )
    }

    # For persons
    OPTIONAL {
        ?uri a schema:Person
        BIND(<https://data.muziekschatten.nl/#personen> AS ?datasetUri).
        OPTIONAL { ?uri schema:name ?schema_name }
        OPTIONAL { ?uri schema:alternateName ?altLabel }
        OPTIONAL { ?uri schema:hasOccupation ?schema_hasOccupation }
        OPTIONAL { ?uri schema:familyName ?schema_familyName }
        OPTIONAL { ?uri schema:givenName ?schema_givenName }
        BIND(
            COALESCE(
                IF(STRLEN(?schema_familyName) > 0 && STRLEN(?schema_givenName) > 0, CONCAT(?schema_familyName, ", ", ?schema_givenName), ?noName),
                ?schema_familyName,
                ?schema_givenName,
                ?schema_name # Fallback
            ) AS ?name
        )
        OPTIONAL { ?uri som:GDAT ?som_birthYear }
        OPTIONAL { ?uri som:SDAT ?som_deathYear }
        BIND(
            COALESCE(
                IF(BOUND(?som_birthYear) && BOUND(?som_deathYear), CONCAT(?som_birthYear, "-", ?som_deathYear), ?noDate),
                IF(BOUND(?som_birthYear), CONCAT(?som_birthYear, "-"), ?noDate),
                IF(BOUND(?som_deathYear), CONCAT("-", ?som_deathYear), ?noDate),
                ""
            ) AS ?dates
        )
        BIND(CONCAT(?name, IF(?dates != "", CONCAT(" (", ?dates, ")"), "")) AS ?prefLabel)
    }
}
LIMIT 1000
