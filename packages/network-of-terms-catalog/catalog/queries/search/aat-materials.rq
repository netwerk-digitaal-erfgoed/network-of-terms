PREFIX aat: <http://vocab.getty.edu/aat/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX luc: <http://www.ontotext.com/owlim/lucene#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX void: <http://rdfs.org/ns/void#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote ;
        rdfs:seeAlso ?rdfs_seeAlso ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:exactMatch ?exactMatch_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
}
WHERE {
    SELECT * WHERE {
        ?uri luc:term ?query ;
            a ?type ;
            void:inDataset <http://vocab.getty.edu/dataset/aat> ;
            gvp:broaderPreferred+ <http://vocab.getty.edu/aat/300010357> . # limit results to the "materials hierarchy name" (300010357)

        ?type rdfs:subClassOf gvp:Subject .
        FILTER (?type != gvp:Subject) .
        ?uri skosxl:prefLabel/skosxl:literalForm ?prefLabel .
        OPTIONAL {
            ?uri skosxl:altLabel/skosxl:literalForm ?altLabel .
        }
        OPTIONAL {
            ?uri skos:scopeNote/rdf:value ?scopeNote .
        }
        OPTIONAL {
            ?uri rdfs:seeAlso ?rdfs_seeAlso
        }
        OPTIONAL {
            ?uri gvp:broaderPreferred ?broader_uri .
            ?broader_uri skosxl:prefLabel/skosxl:literalForm ?broader_prefLabel .
        }
        OPTIONAL {
            ?uri skos:narrower ?narrower_uri .
            ?narrower_uri skosxl:prefLabel/skosxl:literalForm ?narrower_prefLabel .
        }
        OPTIONAL {
            ?uri skos:exactMatch ?exactMatch_uri . # Has no labels.
            FILTER(?exactMatch_uri != ?uri) # Exclude self-reference.
        }
    }
    LIMIT 1000
}
