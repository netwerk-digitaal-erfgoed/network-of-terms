PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:related ?related_uri ;
        skos:exactMatch ?exactMatch_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    ?related_uri skos:prefLabel ?related_prefLabel .
    ?exactMatch_uri skos:prefLabel ?exactMatch_prefLabel .
}
WHERE {
    {
        SELECT * WHERE {
            ?uri a eurovoc:ThesaurusConcept ;
                ?predicate ?label .
            VALUES ?predicate { skos:prefLabel skos:altLabel }

            # TODO: define a search language. For example NL ‘brussel’ should not return ‘bladgroente’ with altLabel ‘Brussels sprouts’.
            FILTER(LANG(?label) = "nl" || LANG(?label) = "en")
            # The http://publications.europa.eu/webapi/rdf/sparql endpoint
            # is based on Virtuoso but it looks like fulltext search support
            # is not enabled therefore fallback to the standard CONTAINS function
            FILTER (CONTAINS(LCASE(STR(?label)), LCASE(?query)))
        }
        #LIMIT#
    }
    OPTIONAL {
        ?uri skos:prefLabel ?prefLabel .
        FILTER(LANG(?prefLabel) = "nl" || LANG(?prefLabel) = "en")
    }
    OPTIONAL {
        ?uri skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl" || LANG(?altLabel) = "en")
    }
    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri skos:prefLabel ?broader_prefLabel .
        FILTER(LANG(?broader_prefLabel) = "nl" || LANG(?broader_prefLabel) = "en")
    }
    OPTIONAL {
        ?uri skos:narrower ?narrower_uri .
        ?narrower_uri skos:prefLabel ?narrower_prefLabel .
        FILTER(LANG(?narrower_prefLabel) = "nl" || LANG(?narrower_prefLabel) = "en")
    }
    OPTIONAL {
        ?uri skos:related ?related_uri .
        ?related_uri skos:prefLabel ?related_prefLabel .
        FILTER(LANG(?related_prefLabel) = "nl" || LANG(?related_prefLabel) = "en")
    }
    OPTIONAL {
        ?uri skos:exactMatch ?exactMatch_uri .
        ?exactMatch_uri skos:exactMatch ?exactMatch_prefLabel .
        FILTER(LANG(?exactMatch_prefLabel) = "nl" || LANG(?exactMatch_prefLabel) = "en")
    }
}
