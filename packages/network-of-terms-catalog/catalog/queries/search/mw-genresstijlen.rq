PREFIX muziekweb: <https://data.muziekweb.nl/vocab/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?rdfs_label ;
        skos:scopeNote ?rdfs_comment .
    ?uri skos:broader ?broader_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?uri skos:narrower ?narrower_uri .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
}
WHERE {
    ?uri a muziekweb:Genre .

    {
        FILTER(?virtuosoQuery)
        ?uri rdfs:label ?rdfs_label .
        ?rdfs_label <bif:contains> ?virtuosoQuery .
    } UNION {
        # BC support.
        BIND(?virtuosoQuery as ?b)
        FILTER(!BOUND(?b))
        ?uri rdfs:label ?rdfs_label .
        # Replace query "A B" with "A AND B", leaving queries "A AND B" or "A OR B" unchanged.
        FILTER (<bif:contains> (?rdfs_label, REPLACE(?query, "(?<!AND)(?<!OR)[[:space:]]+(?!AND)(?!OR)", " AND ", "i")) )
    }

    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri rdfs:label ?broader_prefLabel
    }
    OPTIONAL {
        ?uri skos:narrower ?narrower_uri .
        ?narrower_uri rdfs:label ?narrower_prefLabel
    }
    OPTIONAL { ?uri rdfs:comment ?rdfs_comment }
}
LIMIT 1000
