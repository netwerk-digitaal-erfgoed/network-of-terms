PREFIX muziekweb: <https://data.muziekweb.nl/vocab/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?rdfs_label ;
        skos:scopeNote ?rdfs_comment ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:exactMatch ?exactMatch_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
}
WHERE {
    {
        SELECT DISTINCT ?uri WHERE {
            ?uri a muziekweb:Genre ;
                rdfs:label ?rdfs_label .
            ?rdfs_label <bif:contains> ?virtuosoQuery .
        }
        #LIMIT#
    }

    ?uri  rdfs:label ?rdfs_label .

    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri rdfs:label ?broader_prefLabel
    }
    OPTIONAL {
        ?uri skos:narrower ?narrower_uri .
        ?narrower_uri rdfs:label ?narrower_prefLabel
    }
    OPTIONAL { ?uri rdfs:comment ?rdfs_comment }
    OPTIONAL { ?uri owl:sameAs ?exactMatch_uri } # Has no labels.
}

