PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX justskos: <http://justskos.org/ns/core#>
CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:hiddenLabel ?hiddenLabel ;
        skos:scopeNote ?scopeNote . 

}
WHERE {
    ?uri skos:inScheme <http://data.beeldengeluid.nl/gtaa/Persoonsnamen> .
    ?uri skos:prefLabel|skos:altLabel|skos:hiddenLabel ?label ;
        justskos:status ?status .
        
    FILTER(LANG(?label) = "nl")
    FILTER(?status IN ('approved', 'candidate'))

    # single word query
    FILTER regex(?query, "^[^ ]+$")
    FILTER CONTAINS(LCASE(?label), LCASE(?query))

    ?uri skos:prefLabel ?prefLabel .
    FILTER(LANG(?prefLabel) = "nl" )

    OPTIONAL {
        ?uri skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:hiddenLabel ?hiddenLabel .
        FILTER(LANG(?hiddenLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:scopeNote ?scopeNote .
        FILTER(LANG(?scopeNote) = "nl")
    }
}
LIMIT 50

