PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX justskos: <http://justskos.org/ns/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:hiddenLabel ?hiddenLabel ;
        skos:scopeNote ?scopeNote ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:related ?related_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    ?related_uri skos:prefLabel ?related_prefLabel .
}
WHERE {
    {
        ?uri skos:inScheme <http://data.beeldengeluid.nl/gtaa/Persoonsnamen> .
        ?uri skos:prefLabel|skos:altLabel|skos:hiddenLabel ?label ;
            justskos:status ?status .
        FILTER(LANG(?label) = "nl")
        FILTER(?status IN ('approved', 'candidate'))

        # single word query, match case insensitive
        FILTER regex(?query, "^[^ ]+$")
        FILTER CONTAINS(LCASE(?label), LCASE(?query))
    }
# commented because of timeouts
#    UNION
#    {
#        ?uri skos:inScheme <http://data.beeldengeluid.nl/gtaa/Persoonsnamen> .
#        ?uri ?predicate ?label ;
#            justskos:status ?status .
#        VALUES ?predicate { skos:prefLabel skos:altLabel skos:hiddenLabel }
#        FILTER(LANG(?label) = "nl")
#        FILTER(?status IN ('approved', 'candidate'))
#        
#        # double word query, whitespace separator
#        FILTER REGEX(?query, "^([^ ]+)[ ]+([^ ]+)$")
#
#        BIND(REPLACE(?query, "^([^ ]+)[ ]+([^ ]+)$", "$1") AS ?term1)
#        BIND(REPLACE(?query, "^([^ ]+)[ ]+([^ ]+)$", "$2") AS ?term2)
#
#        # search case insensitive using an AND construct for the query terms
#        FILTER( CONTAINS(LCASE(?label), LCASE(?term1)) && CONTAINS(LCASE(?label), LCASE(?term2)) )
#    }

    OPTIONAL {
        ?uri skos:prefLabel ?prefLabel .
        FILTER(LANG(?prefLabel) = "nl" )
    }
    OPTIONAL {
        ?uri skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:hiddenLabel ?hiddenLabel .
        FILTER(LANG(?hiddenLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:scopeNote ?scopeNote .
        FILTER(LANG(?scopeNote) = "nl")
    }
    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri skos:prefLabel ?broader_prefLabel .
        FILTER(LANG(?broader_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:narrower ?narrower_uri .
        ?narrower_uri skos:prefLabel ?narrower_prefLabel .
        FILTER(LANG(?narrower_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:related ?related_uri .
        ?related_uri skos:prefLabel ?related_prefLabel .
        FILTER(LANG(?related_prefLabel) = "nl")
    }
}
LIMIT 10 
