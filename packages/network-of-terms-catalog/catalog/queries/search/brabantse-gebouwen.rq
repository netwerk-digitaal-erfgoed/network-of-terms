PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?schema_name ;
        skos:altLabel ?schema_alternateName ;
        skos:scopeNote ?schema_description,
                       ?buildingTypeScopeNote,
                       ?addressScopeNote .
}
WHERE {
    ?uri a schema:LandmarksOrHistoricalBuildings .
    ?uri ?predicate ?label .
    VALUES ?predicate { schema:name schema:alternateName }
    FILTER(CONTAINS(LCASE(?label), LCASE(?query)))
    OPTIONAL { ?uri schema:name ?schema_name }
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
    OPTIONAL { ?uri schema:description ?schema_description }
    OPTIONAL {
        ?uri schema:additionalType <http://vocab.getty.edu/aat/300000641> .
        BIND("Type: kloostergebouw" AS ?buildingTypeScopeNote)
    }
    OPTIONAL {
        ?uri schema:address ?address .
        OPTIONAL { ?address schema:streetAddress ?streetAddress }
        OPTIONAL { ?address schema:addressLocality ?addressLocality }
        OPTIONAL { ?address schema:addressRegion ?addressRegion }
        BIND(
            COALESCE(
                IF(?streetAddress && ?addressLocality && ?addressRegion, CONCAT("Adres: ", ?streetAddress, ", ", ?addressLocality, ", ", ?addressRegion), ?noAddress),
                IF(?streetAddress && ?addressLocality, CONCAT("Adres: ", ?streetAddress, ", ", ?addressLocality), ?noAddress),
                IF(?streetAddress, CONCAT("Adres: ", ?streetAddress), ?noAddress),
		        IF(?addressLocality && ?addressRegion, CONCAT("Plaats: ", ?addressLocality, ", ", ?addressRegion), ?noAddress),
        		IF(?addressLocality, CONCAT("Plaats: ", ?addressLocality), ?noAddress)
            ) AS ?addressScopeNote
        )
    }
}
LIMIT 1000
