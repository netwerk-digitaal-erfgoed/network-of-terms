PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?schema_name ;
        skos:altLabel ?schema_alternateName ;
        skos:hiddenLabel ?hiddenLabel;
        skos:scopeNote ?schema_description, ?scopeNote .
}
WHERE {
    ?uri a schema:LandmarksOrHistoricalBuildings .
    ?uri ?predicate ?label .
    VALUES ?predicate { schema:name schema:alternateName }
    FILTER(CONTAINS(LCASE(?label), LCASE(?query)))
    OPTIONAL { ?uri schema:name ?schema_name }
    OPTIONAL { ?uri skos:hiddenLabel ?hiddenLabel }
    OPTIONAL { ?uri skos:scopeNote ?scopeNote}
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
    OPTIONAL { ?uri schema:description ?schema_description }
}
LIMIT 1000
