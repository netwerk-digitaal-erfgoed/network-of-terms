PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX bengthes: <http://data.beeldengeluid.nl/schema/thes#>
PREFIX text: <http://jena.apache.org/text#>
PREFIX vrank: <http://purl.org/voc/vrank#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:hiddenLabel ?hiddenLabel ;
        skos:scopeNote ?scopeNote ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:related ?related_uri ;
        skos:exactMatch ?exactMatch_uri ;
        vrank:simpleRank ?score .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    ?related_uri skos:prefLabel ?related_prefLabel .
    ?exactMatch_uri skos:prefLabel ?exactMatch_prefLabel .
}
WHERE {
    {
        SELECT ?uri (MAX(?sc) as ?score) {
            (?uri ?sc) text:query (skos:prefLabel skos:altLabel skos:hiddenLabel ?query) .
            ?uri skos:inScheme ?datasetUri ;
                bengthes:status ?status .
            FILTER(?status IN ('approved', 'candidate'))
        }
        GROUP BY ?uri
        #LIMIT#
    }

    OPTIONAL {
        ?uri skos:prefLabel ?prefLabel .
        FILTER(LANG(?prefLabel) = "nl" )
    }
    OPTIONAL {
        ?uri skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:hiddenLabel ?hiddenLabel .
        FILTER(LANG(?hiddenLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:scopeNote ?scopeNote .
        FILTER(LANG(?scopeNote) = "nl")
    }
    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri skos:prefLabel ?broader_prefLabel .
        FILTER(LANG(?broader_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:narrower ?narrower_uri .
        ?narrower_uri skos:prefLabel ?narrower_prefLabel .
        FILTER(LANG(?narrower_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:related ?related_uri .
        ?related_uri skos:prefLabel ?related_prefLabel .
        FILTER(LANG(?related_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:exactMatch ?exactMatch_uri .
        ?exactMatch_uri skos:prefLabel ?exactMatch_prefLabel .
        FILTER(LANG(?exactMatch_prefLabel) = "nl")
    }
}
