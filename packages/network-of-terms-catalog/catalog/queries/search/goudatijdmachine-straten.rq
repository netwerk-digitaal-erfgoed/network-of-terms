PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gtm: <https://www.goudatijdmachine.nl/def#>
PREFIX sdo: <https://schema.org/>
PREFIX hg: <http://rdf.histograph.io/>
PREFIX luc: <http://www.ontotext.com/connectors/lucene#>
PREFIX luc-index: <http://www.ontotext.com/connectors/lucene/instance#>
PREFIX omeka: <http://omeka.org/s/vocabs/o#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX o: <http://omeka.org/s/vocabs/o#>

CONSTRUCT {
    ?piduri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel;
        skos:scopeNote ?scopeNote ;
        skos:related ?related_uri ;
        skos:exactMatch ?exactMatch_uri .
    ?related_uri skos:prefLabel ?related_prefLabel .
    ?exactMatch_uri skos:prefLabel ?exactMatch_label .
} WHERE {
    ?search a luc-index:straten_index ;
        luc:query ?query ;
        luc:entities ?uri .

    ?uri sdo:identifier ?pid
    FILTER(STRSTARTS(?pid, "https://n2t.net"))
    BIND(IRI(?pid) AS ?piduri)

    ?uri sdo:name ?prefLabel ;
        luc:score ?score .

    OPTIONAL {
        ?uri gtm:genoemdNaar ?genoemdNaar .
    }

    BIND (
        CONCAT(
            IF ( EXISTS { ?entity omeka:item_set <https://n2t.net/ark:/60537/bd75pg> }, "Verdwenen straat", "Straat" ),
            IF ( BOUND(?genoemdNaar), CONCAT(" in Gouda, genoemd naar ", ?genoemdNaar )," in Gouda" )
        )
    AS ?scopeNote )

    OPTIONAL { ?uri sdo:alternateName ?altLabel . }

    OPTIONAL {
        ?uri hg:absorbedBy|hg:absorbed ?related_uri .
        ?related_uri a gtm:Straat ;
            sdo:name ?related_prefLabel .
        FILTER (STRSTARTS(STR(?related_uri), "https://n2t.net/ark:/60537/") && ?uri != ?related_uri )
    }

    OPTIONAL {
        ?uri rdfs:seeAlso ?exactMatch_uri .
        ?exactMatch_uri o:label ?exactMatch_label .
    }
} ORDER BY DESC(?score) LIMIT 100
