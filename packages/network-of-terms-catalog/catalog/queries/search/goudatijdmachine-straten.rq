PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gtm: <https://www.goudatijdmachine.nl/def#>
PREFIX sdo: <https://schema.org/>
PREFIX hg: <http://rdf.histograph.io/>
PREFIX luc: <http://www.ontotext.com/connectors/lucene#>
PREFIX luc-index: <http://www.ontotext.com/connectors/lucene/instance#>
PREFIX omeka: <http://omeka.org/s/vocabs/o#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel;
        skos:scopeNote ?scopeNote ;
        skos:related ?related_uri .
    ?related_uri skos:prefLabel ?related_prefLabel .
} WHERE {
    ?search a luc-index:straten_index ;
        luc:query ?query ;
        luc:entities ?uri .

    FILTER (STRSTARTS(STR(?uri), "https://n2t.net/ark:/60537/"))
       
    ?uri sdo:identifier ?identifier ;
        sdo:name ?prefLabel ;
        luc:score ?score .

    BIND ( CONCAT( IF ( EXISTS { ?entity omeka:item_set <https://n2t.net/ark:/60537/bd75pg> }, "Verdwenen straat", "Straat" )," in Gouda") AS ?scopeNote )
    
    OPTIONAL { ?uri sdo:alternateName ?altLabel . }

    OPTIONAL {
        ?uri hg:absorbedBy|hg:absorbed ?related_uri .
        ?related_uri a gtm:Straat ;
                sdo:name ?related_prefLabel .
        FILTER (STRSTARTS(STR(?related_uri), "https://n2t.net/ark:/60537/") && ?uri != ?related_uri )
    }
} ORDER BY DESC(?score) LIMIT 100