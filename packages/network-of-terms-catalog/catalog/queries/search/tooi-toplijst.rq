PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:related ?related_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    ?related_uri skos:prefLabel ?related_prefLabel . 
}
WHERE {
    ?uri skos:inScheme <https://identifier.overheid.nl/tooi/def/thes/top/toplijst> .
    ?uri ?predicate ?label .
    VALUES ?predicate { skos:prefLabel skos:altLabel }
    FILTER(LANG(?label) = "nl")
    # just using default SPARQL search because of the small size of the dataset
    FILTER (CONTAINS(LCASE(STR(?label)),LCASE(?query)))
    OPTIONAL {
        ?uri skos:prefLabel ?prefLabel .
    }
    OPTIONAL {
        ?uri skos:altLabel ?altLabel .
    }
    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri skos:prefLabel ?broader_prefLabel .
    }
    OPTIONAL {
        ?narrower_uri skos:broader ?uri .
        ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    }
    OPTIONAL {
        ?uri skos:related ?related_uri .
        ?related_uri skos:prefLabel ?related_prefLabel .
    }
}
LIMIT 1000