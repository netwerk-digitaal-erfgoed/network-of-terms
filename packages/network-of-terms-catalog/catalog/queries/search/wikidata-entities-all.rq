PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX mwapi: <https://www.mediawiki.org/ontology#API/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vrank: <http://purl.org/voc/vrank#>
PREFIX wikibase: <http://wikiba.se/ontology#>

CONSTRUCT {
    ?item a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?description ;
        vrank:simpleRank ?score .
}
WHERE {
    SERVICE wikibase:mwapi {
        bd:serviceParam wikibase:endpoint "www.wikidata.org" ;
            wikibase:api "EntitySearch" ;
            mwapi:language "nl" ;
            mwapi:search ?query ;
            wikibase:limit ?limit .
        ?item wikibase:apiOutputItem mwapi:item .
        ?ordinal wikibase:apiOrdinal true.
    }
    ?item rdfs:label ?prefLabel .
    FILTER(LANG(?prefLabel) = "nl" || LANG(?prefLabel) = "en")
    OPTIONAL {
        ?item skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl" || LANG(?altLabel) = "en")
    }
    OPTIONAL {
        ?item schema:description ?description .
        FILTER(LANG(?description) = "nl" || LANG(?description) = "en")
    }
    BIND(-?ordinal AS ?score)
}
ORDER BY ASC(?ordinal)

