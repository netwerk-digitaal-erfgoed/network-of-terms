PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX mwapi: <https://www.mediawiki.org/ontology#API/>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wikibase: <http://wikiba.se/ontology#>

CONSTRUCT {
    # Wikidata seems to have a problem with the short syntax for construct queries
    # so always use full syntax: "?s ?p ?o ." else you end up with an empty result list!
    ?uri a skos:Concept .
    ?uri skos:prefLabel ?rdfs_label .
    ?uri skos:altLabel ?altLabel .
    ?uri skos:scopeNote ?schema_description .
}
WHERE {
    SERVICE wikibase:mwapi {
        bd:serviceParam wikibase:endpoint "www.wikidata.org" .
        bd:serviceParam wikibase:api "EntitySearch" .
        bd:serviceParam mwapi:language "nl" .
        bd:serviceParam mwapi:search ?query .
        ?item wikibase:apiOutputItem mwapi:item .
    }
    ?item rdfs:label ?rdfs_label .
    
    # replace HTTP with HTTPS in ?item because of current poor Wikidata support for HTTP
    BIND(URI(REPLACE(LCASE(STR(?item)),"http://","https://")) as ?uri)
    FILTER(LANG(?rdfs_label) = "nl")
    OPTIONAL {
        ?item skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl")
    }
    OPTIONAL {
        ?item schema:description ?schema_description
        FILTER(LANG(?schema_description) = "nl")
    }
}
LIMIT 1000
