PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX void: <http://rdfs.org/ns/void#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?rdfs_label ;
        skos:altLabel ?schema_name ;
        skos:altLabel ?schema_alternateName ;
        skos:scopeNote ?scopeNote ;
        skos:exactMatch ?exactMatch_uri .
}
WHERE {
    ?uri schema:mainEntityOfPage/schema:isPartOf <http://data.bibliotheken.nl/id/dataset/stcn/printers> ;
        schema:additionalType <http://www.productontology.org/id/Printer_%28publishing%29> ;   # Select printers.
        rdfs:label ?rdfs_label .

    ?uri ?predicate ?label .
    VALUES ?predicate { rdfs:label schema:name schema:alternateName }
    ?label <bif:contains> ?virtuosoQuery .
    # STCN defines multiple URIs for the same printer.
    # The distinction between these URIs is based on the place where the printer lived and worked.
    # See for example "Plantijn"
    # http://data.bibliotheken.nl/id/thes/p075556251 for Plantijn working in Leiden
    # http://data.bibliotheken.nl/id/thes/p338012834 for Plantijn working in Antwerp
    # The original source (STCN) shows also a time period, this data currently not available in the LOD version.
    OPTIONAL {
     ?uri schema:location/schema:address/schema:addressLocality ?scopeNote
    }
    OPTIONAL { ?uri schema:name ?schema_name }
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
    OPTIONAL {
        ?uri schema:sameAs ?exactMatch_uri . # Has no labels.
    }
}
LIMIT 1000
