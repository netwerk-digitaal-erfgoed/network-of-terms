PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gn: <https://www.geonames.org/ontology#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel_ext ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote ;
        skos:broader ?broader .
    ?broader skos:prefLabel ?broader_prefLabel .
}
WHERE {
    ?uri a gn:Feature ;
        gn:featureClass ?featureClass ;
        gn:name ?prefLabel ;
        gn:countryCode ?countryCode ;
        ?names ?name .

    BIND(CONCAT(?prefLabel," (",UCASE(?countryCode),")") as ?prefLabel_ext) 

    # Search in names and alternative names.
    VALUES ?names { gn:name gn:alternateName }

    # Limit results to places (P), localities (L), administrative levels (A) and water surfaces (H).
    VALUES ?featureClass { gn:P gn:L gn:A gn:H }

    # Select results that start with the search string
    # Using contains() leads to too much noise 
    FILTER(STRSTARTS(LCASE(?name), LCASE($query)))

    OPTIONAL {
        ?uri gn:alternateName ?altLabel .
        FILTER(?altLabel != "")
    }

    OPTIONAL {
        ?uri ?parents ?broader .
        ?broader gn:name ?broader_prefLabel
        VALUES ?parents { gn:parentADM1 gn:parentADM2 }
        # filter out the circular links in the converted data
        FILTER(?uri != ?broader)
    }

    OPTIONAL {
        ?uri gn:featureCode/gn:name ?scopeNote .
    }
}
LIMIT 1000
