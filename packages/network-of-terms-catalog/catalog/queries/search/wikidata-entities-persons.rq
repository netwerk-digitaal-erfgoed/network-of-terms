PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX mwapi: <https://www.mediawiki.org/ontology#API/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vrank: <http://purl.org/voc/vrank#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

CONSTRUCT {
    ?item a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?description ;
        vrank:simpleRank ?score .
}
WHERE {
    {
        SELECT DISTINCT ?item ?score WHERE {
            SERVICE wikibase:mwapi {
                bd:serviceParam wikibase:endpoint "www.wikidata.org" ;
                    wikibase:api "Search" ;
                    mwapi:language "nl" ;
                    mwapi:srsearch ?query .
                ?item wikibase:apiOutputItem mwapi:title .
                ?ordinal wikibase:apiOrdinal true.
            }

            BIND(-?ordinal AS ?score)
            # Only select instances of type "human"
            ?item wdt:P31 wd:Q5 .
        }
        ORDER BY ASC(?ordinal)
        #LIMIT#
    }

    OPTIONAL {
        ?item rdfs:label ?prefLabel .
        FILTER(LANG(?prefLabel) = "nl" || LANG(?prefLabel) = "en")
    }
    OPTIONAL {
        ?item skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl" || LANG(?altLabel) = "en")
    }
    OPTIONAL {
        ?item schema:description ?description .
        FILTER(LANG(?description) = "nl" || LANG(?description) = "en")
    }
}
