# do not remove this prefix - KB data uses old version of SKOS
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT { 
  ?uri a skos:Concept ; 
       skos:prefLabel ?rdfs_label ; 
       skos:altLabel ?schema_name ;
       skos:altLabel ?schema_alternateName ;
       skos:exactMatch ?exactMatch ; 
       skos:broader ?broader_uri ;
       skos:related ?related_uri ;
       skos:narrower ?narrower_uri ;
       skos:scopeNote ?scopeNote .

   ?broader_uri skos:prefLabel ?broader_prefLabel .
   ?narrower_uri skos:prefLabel ?narrower_prefLabel .
   ?related_uri skos:related ?related_prefLabel .
}
WHERE { 
  ?uri a skos:Concept .
  ?uri skos:inScheme <http://data.bibliotheken.nl/id/scheme/brinkman> .
  ?uri ?predicate ?label .

  # skos:prefLabel is ignored here because its values seem to be equal to rfds:label
  VALUES ?predicate { rdfs:label schema:name schema:alternateName }
    
  # Replace query "A B" with "A AND B", leaving queries "A AND B" or "A OR B" unchanged.
  FILTER (<bif:contains> (?label, REPLACE(?query, "(?<!AND)(?<!OR)[[:space:]]+(?!AND)(?!OR)", " AND ", "i")))

  OPTIONAL { ?uri rdfs:label ?rdfs_label }
  OPTIONAL { ?uri schema:name ?schema_name }
  OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
  OPTIONAL { ?uri skos:exactMatch ?exactMatch }
  OPTIONAL { 
    ?uri skos:broader ?broader_uri .
    ?broader_uri rdfs:label ?broader_prefLabel . 
  }
  OPTIONAL { 
    ?uri skos:narrower ?narrower_uri .
    ?narrower_uri rdfs:label ?narrower_prefLabel . 
  }
  OPTIONAL {
    ?uri skos:related ?related_uri .
    ?related_uri rdfs:label ?related_prefLabel .
  }
  OPTIONAL { ?uri rdfs:comment ?scopeNote }
  OPTIONAL { ?uri schema:alternateName | schema:name ?altLabel }
}
LIMIT 1000