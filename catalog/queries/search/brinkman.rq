# Do not remove this prefix - KB data uses old version of SKOS
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:hiddenLabel ?hiddenLabel ;
        skos:scopeNote ?scopeNote ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:related ?related_uri ;
        skos:exactMatch ?exactMatch .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    ?related_uri skos:prefLabel ?related_prefLabel .
}
WHERE {
    ?uri a skos:Concept .
    ?uri skos:inScheme <http://data.bibliotheken.nl/id/scheme/brinkman> .
    ?uri ?predicate ?label .
    VALUES ?predicate { skos:prefLabel skos:altLabel }
    FILTER(LANG(?label) = "nl")

    {
        FILTER(?booleanQuery)
        ?uri ?predicate ?label .
        ?label <bif:contains> ?booleanQuery .
    } UNION {
        # BC support.
        BIND(?booleanQuery as ?b)
        FILTER(!BOUND(?b))
        ?uri ?predicate ?label .
        # Replace query "A B" with "A AND B", leaving queries "A AND B" or "A OR B" unchanged.
        FILTER (<bif:contains> (?label, REPLACE(?query, "(?<!AND)(?<!OR)[[:space:]]+(?!AND)(?!OR)", " AND ", "i")))
    }

    OPTIONAL {
        ?uri skos:prefLabel ?prefLabel .
        FILTER(LANG(?prefLabel) = "nl" )
    }
    OPTIONAL {
        ?uri skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:hiddenLabel ?hiddenLabel .
        FILTER(LANG(?hiddenLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:scopeNote ?scopeNote .
        FILTER(LANG(?scopeNote) = "nl")
    }
    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri skos:prefLabel ?broader_prefLabel .
        FILTER(LANG(?broader_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:narrower ?narrower_uri .
        ?narrower_uri skos:prefLabel ?narrower_prefLabel .
        FILTER(LANG(?narrower_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:related ?related_uri .
        ?related_uri skos:prefLabel ?related_prefLabel .
        FILTER(LANG(?related_prefLabel) = "nl")
    }
    OPTIONAL { ?uri skos:exactMatch ?exactMatch }
}
LIMIT 1000
