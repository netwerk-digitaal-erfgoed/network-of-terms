PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?rdfs_label ;
        skos:altLabel ?schema_name ;
        skos:altLabel ?schema_alternateName ;
        skos:scopeNote ?schema_description .
}
WHERE {
    ?uri schema:mainEntityOfPage/schema:isPartOf <http://data.bibliotheken.nl/id/dataset/persons> .
    ?uri ?predicate ?label .
    VALUES ?predicate { rdfs:label schema:name schema:alternateName }

    {
        FILTER(?virtuosoQuery)
        ?uri ?predicate ?label .
        ?label <bif:contains> ?virtuosoQuery .
    } UNION {
        # BC support.
        BIND(?virtuosoQuery as ?b)
        FILTER(!BOUND(?b))
        ?uri ?predicate ?label .
        # Replace query "A B" with "A AND B", leaving queries "A AND B" or "A OR B" unchanged.
        FILTER (<bif:contains> (?label,
            REPLACE(
                REPLACE(?query, "[.,]", " "),
                "(?<!AND)(?<!OR)[[:space:]]+(?!AND)(?!OR)(?!$)(?![[:space:]])",
                " AND ",
                "i"
            )
        ))
    }

    OPTIONAL { ?uri rdfs:label ?rdfs_label }
    OPTIONAL { ?uri schema:name ?schema_name }
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
    OPTIONAL { ?uri schema:description ?schema_description }
}
LIMIT 1000
