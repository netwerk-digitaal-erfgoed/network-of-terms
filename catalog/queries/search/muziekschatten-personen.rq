PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX som: <http://data.muziekschatten.nl/som/>

# Persons for testing the query
# No birth year:
# <https://data.muziekschatten.nl/som/da6866c6ba20fb11f6e2617d4a971b91>
# No death year:
# <https://data.muziekschatten.nl/som/ae7b2de42c4158219176d464008dfd49>
# No birth year, no death year:
# <https://data.muziekschatten.nl/som/ed25d29466903394c982dd3dc185bd80>
# Both a birth year and a death year:
# <https://data.muziekschatten.nl/som/ff20e6a4c17dbb9cddbffedc97c292e0>
# No family name:
# <https://data.muziekschatten.nl/som/4275e51d7d94b224928da9b7707a170a>
# No given name:
# <https://data.muziekschatten.nl/som/b101ea3b20de7f9b40070ea9191d98bd>
# No family name, no given name:
# <https://data.muziekschatten.nl/som/c5f95197941d9bb4f68a1e25c379f081>
# Both a family name and a given name:
# <https://data.muziekschatten.nl/som/b7bc6d966287e0f0e0f491752ff65ba5>

CONSTRUCT {
    ?uri a skos:Concept;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?schema_alternateName ;
        skos:scopeNote ?schema_hasOccupation .
}
WHERE {
    ?uri a schema:Person .
    # Require the presence of this property for quality purposes
    ?uri som:NDEREC ?nderec .

    ?uri ?predicate ?label .
    VALUES ?predicate { schema:name schema:alternateName }

    # A 'FILTER (<bif:contains>, ...)' currently does not work due to https://github.com/comunica/comunica/issues/844
    ?label <bif:contains> ?query .

    OPTIONAL { ?uri schema:name ?schema_name }
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName }
    OPTIONAL { ?uri schema:hasOccupation ?schema_hasOccupation }

    OPTIONAL { ?uri schema:familyName ?schema_familyName }
    OPTIONAL { ?uri schema:givenName ?schema_givenName }
    BIND(
        COALESCE(
            IF(BOUND(?schema_familyName) && BOUND(?schema_givenName), CONCAT(?schema_familyName, ", ", ?schema_givenName), ?noName),
            ?schema_familyName,
            ?schema_givenName,
            ?schema_name # Fallback
        ) AS ?name
    )
    OPTIONAL { ?uri som:GDAT ?som_birthYear }
    OPTIONAL { ?uri som:SDAT ?som_deathYear }
    BIND(
        COALESCE(
            IF(BOUND(?som_birthYear) && BOUND(?som_deathYear), CONCAT(?som_birthYear, "-", ?som_deathYear), ?noDate),
            IF(BOUND(?som_birthYear), CONCAT(?som_birthYear, "-"), ?noDate),
            IF(BOUND(?som_deathYear), CONCAT("-", ?som_deathYear), ?noDate),
            ""
        ) AS ?dates
    )
    BIND(CONCAT(?name, IF(?dates != "", CONCAT(" (", ?dates, ")"), "")) AS ?prefLabel)
}
LIMIT 1000
