PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
    skos:prefLabel ?schema_name ;
    skos:altLabel ?schema_alternateName ;
    skos:scopeNote ?schema_description .
}
WHERE {
    ?uri a ?type .
    VALUES ?type { schema:Person schema:Organization }
    ?uri ?name ?label .
    VALUES ?name { schema:name schema:alternateName }

    # Replace query "A B" with "A AND B", leaving queries "A AND B" or "A OR B" unchanged.
    FILTER (<bif:contains> (?label, REPLACE(?query, "(?<!AND)(?<!OR)[[:space:]]+(?!AND)(?!OR)", " AND ", "i")))

    OPTIONAL { ?uri schema:name ?schema_name . }
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName . }
    OPTIONAL { ?uri schema:description ?schema_description . }
}
LIMIT 1000
