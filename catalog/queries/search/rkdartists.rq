PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?schema_name ;
        skos:altLabel ?schema_alternateName ;
        skos:scopeNote ?schema_description ;
    # commented the following lines as a temporary fix
    #    rdfs:seeAlso ?rdfs_seeAlso .
}
WHERE {
    ?uri a ?type .
    VALUES ?type { schema:Person schema:Organization }
    ?uri ?name ?label .
    VALUES ?name { schema:name schema:alternateName }

    {
        FILTER(?virtuosoQuery)
        ?uri ?predicate ?label .
        ?label <bif:contains> ?virtuosoQuery .
    } UNION {
        # BC support.
        # The RKD Virtuoso endpoint needs isLiteral() rather than !BOUND() because the latter still matches, throwing an
        # error for queries with diacritics.
        FILTER(!isLiteral(?virtuosoQuery))

        ?uri ?predicate ?label .
        # Replace query "A B" with "A AND B", leaving queries "A AND B" or "A OR B" unchanged.
        FILTER (<bif:contains> (?label,
            REPLACE(
                REPLACE(?query, "[.,]", " "),
                "(?<!AND)(?<!OR)[[:space:]]+(?!AND)(?!OR)(?!$)(?![[:space:]])",
                " AND ",
                "i"
            )
        ))
    }

    OPTIONAL { ?uri schema:name ?schema_name . }
    OPTIONAL { ?uri schema:alternateName ?schema_alternateName . }
    OPTIONAL { ?uri schema:description ?schema_description . }

    # commented the following lines as a temporary fix
    #BIND(STRAFTER(STR(?uri), "https://data.rkd.nl/artists/") AS ?identifier)
    #BIND(IRI(CONCAT("https://rkd.nl/explore/artists/", ?identifier)) AS ?rdfs_seeAlso)
}
LIMIT 1000
