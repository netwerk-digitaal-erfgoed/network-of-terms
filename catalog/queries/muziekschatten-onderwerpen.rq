PREFIX som: <http://data.muziekschatten.nl/som/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  ?uri a skos:Concept; 
       skos:prefLabel ?schema_name;
       skos:scopeNote ?scopeNote;
       skos:broader ?uri_broader .

  ?uri_broader skos:prefLabel ?broader_schema_name .
}
WHERE {
  graph <http://data.muziekschatten.nl/somlod/> {

  ?uri a skos:Concept .
  ?uri ?predicate ?label .
  VALUES ?predicate { schema:name schema:keywords }

  # Replace query "A B" with "A AND B", leaving queries "A AND B" or "A OR B" unchanged.
  FILTER (<bif:contains> (?label, REPLACE(?query, "(?<!AND)(?<!OR)[[:space:]]+(?!AND)(?!OR)", " AND ", "i")))
  OPTIONAL {
   ?uri schema:name ?schema_name .
   FILTER (langMatches(lang(?schema_name), "nl")) 
  }
  OPTIONAL {
   ?uri schema:keywords ?keywords
   BIND(CONCAT("Trefwoorden: ",?keywords ) as ?keywords_note)
  }

  OPTIONAL {
    ?uri skos:broader ?uri_broader .
    ?uri_broader schema:name ?broader_schema_name .
    FILTER (langMatches(lang(?broader_schema_name),"nl"))
  }
  }
  graph <http://data.muziekschatten.nl/som/> {
    ?uri som:BASIS "1" .
    ?uri som:DC ?dc
    BIND(CONCAT("Dewey: ",?dc) as ?dewey_note)
  }
  BIND( CONCAT(?keywords_note," | ",?dewey_note) as ?scopeNote)
} LIMIT 1000