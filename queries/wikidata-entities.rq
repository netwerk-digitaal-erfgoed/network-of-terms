PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX mwapi: <https://www.mediawiki.org/ontology#API/>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wikibase: <http://wikiba.se/ontology#>

CONSTRUCT {
    ?item a skos:Concept ;
    skos:prefLabel ?rdfs_label ;
    skos:altLabel ?altLabel ;
    skos:scopeNote ?schema_description .
}
WHERE {
    SERVICE wikibase:mwapi {
        bd:serviceParam wikibase:endpoint "www.wikidata.org" ;
        wikibase:api "EntitySearch" ;
        mwapi:search ?query ;
        mwapi:language "nl" .
        ?item wikibase:apiOutputItem mwapi:item .
    }
    ?item rdfs:label ?rdfs_label .
    FILTER(LANG(?rdfs_label) = "nl")
    OPTIONAL {
        ?item skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl")
    }
    OPTIONAL {
        ?item schema:description ?schema_description
        FILTER(LANG(?schema_description) = "nl")
    }
}
LIMIT 1000
